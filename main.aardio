//main.aardio
import win.ui;
import win.ui.mask;
import win;
import win.clip;
import winex;
import winex.key;
import winex.mouse;
import web.form;
import console;
import config;
import process;
import thread;
import thread.command;
import fsys.config;
import fsys.ini;
import seiki;
import seiki.game;
import seiki.swarm;
import seiki.gsconfig;
import seiki.gsonline;
import inet.ras;
import preg;
/*DSG{{*/
mainForm = win.form(text="SeikiGSTool";right=743;bottom=399)
mainForm.add(
button_game_size={cls="button";text="应用";left=640;top=168;right=720;bottom=200;ah=1;aw=1;z=23};
button_game_start={cls="button";text="开始游戏";left=600;top=216;right=728;bottom=248;ah=1;aw=1;font=LOGFONT(weight=700);z=17};
button_mod_manage={cls="button";text="管理MOD";left=272;top=352;right=376;bottom=384;ah=1;aw=1;z=9};
button_mod_ok={cls="button";text="确定";left=384;top=352;right=488;bottom=384;ah=1;aw=1;z=8};
button_online_connect={cls="button";text="一键开房";left=512;top=96;right=640;bottom=128;ah=1;aw=1;z=13};
button_online_copy={cls="button";text="复制";left=648;top=96;right=728;bottom=128;ah=1;aw=1;z=15};
button_setting={cls="button";text="设置";left=624;top=264;right=728;bottom=296;ah=1;aw=1;z=20};
button_setting_key={cls="button";text="键位设置";left=512;top=264;right=616;bottom=296;ah=1;aw=1;z=18};
checkbox_replay={cls="checkbox";text="记录Replay";left=512;top=224;right=598;bottom=240;disabled=1;hide=1;z=19};
combobox_game_size={cls="combobox";left=520;top=176;right=632;bottom=200;edge=1;items={};mode="dropdown";z=22};
edit_mod_name={cls="edit";left=280;top=72;right=480;bottom=96;ah=1;autohscroll=false;aw=1;bgcolor=16777215;edge=1;readonly=1;z=5};
edit_online_ip={cls="edit";left=512;top=56;right=728;bottom=88;ah=1;align="center";aw=1;bgcolor=16777215;edge=1;font=LOGFONT(h=-16);readonly=1;z=14};
form_web={cls="custom";text="自定义控件";left=8;top=8;right=256;bottom=392;ah=1;aw=1;edge=1;z=25};
groupbox_game={cls="groupbox";text="第三步：圆神启动";left=504;top=136;right=736;bottom=256;ah=1;aw=1;edge=1;z=16};
groupbox_game_size={cls="groupbox";text="调整窗口大小";left=512;top=152;right=728;bottom=208;ah=1;aw=1;edge=1;z=21};
groupbox_mod={cls="groupbox";text="第一步：选择一个MOD";left=264;top=8;right=496;bottom=392;ah=1;aw=1;edge=1;z=1};
groupbox_mod_name={cls="groupbox";text="MOD名称";left=272;top=56;right=488;bottom=104;ah=1;aw=1;edge=1;z=4};
groupbox_mod_viewer={cls="groupbox";text="MOD选择与预览";left=272;top=104;right=488;bottom=344;ah=1;aw=1;edge=1;z=6};
groupbox_online={cls="groupbox";text="第二步：玩单机还是联机";left=504;top=8;right=736;bottom=136;ah=1;aw=1;edge=1;z=10};
listbox_mod_viewer={cls="listbox";left=280;top=120;right=480;bottom=336;ah=1;aw=1;edge=1;hscroll=1;items={};vscroll=1;z=7};
picturebox={cls="picturebox";left=504;top=296;right=736;bottom=392;notify=1;z=24};
plus={cls="plus";left=0;top=0;right=1;bottom=1;clipBk=false;z=26};
radiobutton_offline={cls="radiobutton";text="单机";left=512;top=32;right=576;bottom=48;ah=1;aw=1;z=11};
radiobutton_online={cls="radiobutton";text="联机";left=600;top=32;right=696;bottom=48;ah=1;aw=1;z=12};
radiobutton_use_mod={cls="radiobutton";text="玩MOD";left=360;top=32;right=456;bottom=48;ah=1;aw=1;z=3};
radiobutton_without_mod={cls="radiobutton";text="玩原生";left=272;top=32;right=336;bottom=48;ah=1;aw=1;z=2};
static_warning={cls="static";text="Loading...";left=512;top=312;right=728;bottom=384;transparent=1;z=27}
)
/*}}*/

if !io.exist(global.path_game) 	{ 
	win.msgbox("请将该程序置于游戏根目录。", mainForm.text,, mainForm.hwnd) return ; 
}
if !io.exist(global.path_swarm) { 
	win.msgbox("暂未下载Swarm，开房功能将无法使用", mainForm.text,, mainForm.hwnd) 
}

if !_STUDIO_INVOKED try {
	var t = time().setMilliTime(tonumber(string.load("/res/build.date")))
	if (t.diffsecond(time.now().addday(-3)) < 0) {
		if !win.msgboxTest('该版本程序已过期！\n无论如何都要使用该程序吗？（不推荐）', mainForm.text, mainForm.hwnd) return ; 
	}
	t.format = "编译于 %Y/%m/%d %H:%M:%S"
	mainForm.static_warning.text = tostring(t) + '\n当前版本仅供测试，3天后将自动失效\n（这里会放一张轮回群的宣传图）'
}

seiki.set_font(mainForm)
seiki.init()
//console.open()

wan, lan = inet.ras.isAlive()
is_alive = true

if (wan === null && lan === null) {
	win.msgbox("请在联网的情况下使用该程序。开房功能已禁用。", mainForm.text,, mainForm.hwnd)
	mainForm.text += " [离线模式]"
	is_alive = false
}

win.setTimeout(
	function(){
		wb = web.form(mainForm.form_web)
		wb.external = { tip = function(cls) seiki.circle(mainForm, mainForm[cls]) }
		if (is_alive) wb.go("http://seiki.fun:8008/api/gs/v1/index.html") 
		else wb.html = /**<meta charset="utf-8">您暂未联网，无法使用导航功能。**/
		wb.showMsg = lambda() false
		wb.showMenu = lambda() false

	}
)


//mainForm.plus.z = 100

mainForm.radiobutton_without_mod.checked = true

mainForm.listbox_mod_viewer.items = seiki.get_mods_names()
if (#mainForm.listbox_mod_viewer.items == 0) mainForm.edit_mod_name.text = "请单击“管理MOD”来添加MOD"

mainForm.listbox_mod_viewer.onSelChange = function() 
	mainForm.edit_mod_name.text = mainForm.listbox_mod_viewer.selText ++ ".dat"

mainForm.button_mod_manage.oncommand = function(id,event) {
	seiki.setting_open(mainForm)
	mainForm.listbox_mod_viewer.items = seiki.get_mods_names()
}

global.step1 = false;

mainForm.update = function() {
	var without_mod = mainForm.radiobutton_without_mod.checked;
	var offline = mainForm.radiobutton_offline.checked;
		
	mainForm.radiobutton_use_mod.disabled = global.step1
	mainForm.radiobutton_without_mod.disabled = global.step1 
	mainForm.edit_mod_name.disabled = global.step1
	mainForm.listbox_mod_viewer.disabled = global.step1
	mainForm.button_mod_manage.disabled = global.step1
	mainForm.button_mod_ok.text = global.step1? "解锁": "确定"

	mainForm.button_mod_ok.setFont(bold = !global.step1);
	mainForm.button_game_start.setFont(bold = global.step1);
	mainForm.button_mod_ok.redraw()
	mainForm.button_game_start.redraw()
	
	mainForm.radiobutton_online.disabled = !global.step1	
	mainForm.radiobutton_offline.disabled = !global.step1	
	mainForm.edit_online_ip.disabled = !global.step1
	mainForm.button_online_connect.disabled = !global.step1
	mainForm.button_online_copy.disabled = !global.step1
	mainForm.combobox_game_size.disabled = !global.step1
	mainForm.button_game_size.disabled = !global.step1
	mainForm.checkbox_replay.disabled = !global.step1
	mainForm.button_game_start.disabled = !global.step1
	
	if without_mod {
		mainForm.edit_mod_name.disabled = true
		mainForm.listbox_mod_viewer.disabled = true
		//mainForm.button_mod_manage.disabled = true		
	}
	if offline || !is_alive {
		mainForm.edit_online_ip.disabled = true
		mainForm.button_online_connect.disabled = true
		mainForm.button_online_copy.disabled = true		
	}
}


mainForm.button_mod_ok.oncommand = function(id,event){
	global.step1 = !global.step1
	mainForm.update()
}
mainForm.update()

mainForm.radiobutton_offline.checked = true

//winform.edit.text = 'gs03滥竽充数1号.dat\r\n这里放该MOD滴介绍'
for (_, v in seiki.size_list) if v.height <= seiki.display.height mainForm.combobox_game_size.add(v.name)
mainForm.combobox_game_size.selIndex = 2

mainForm.radiobutton_without_mod.oncommand = function() mainForm.update()
mainForm.radiobutton_use_mod.oncommand = function() mainForm.update()
mainForm.radiobutton_offline.oncommand = function() mainForm.update()
mainForm.radiobutton_online.oncommand = function() mainForm.update()

mainForm.button_setting.oncommand = function(id, event) {
	seiki.setting_open(mainForm)
	mainForm.listbox_mod_viewer.items = seiki.get_mods_names()
}

mainForm.button_online_connect.oncommand = function(id,event){
	try { 
		swarm.open()
	}
}

mainForm.button_online_copy.oncommand = function(id,event){
	var ip = mainForm.edit_online_ip.text
	if (#ip > 0) {
		var text = config.online.share ? config.online.share_l + ip + config.online.share_r : ip
		win.clip.write(text)
	}
}


mainForm.show();
//console.open()

swarm = null
game = {}
gsonline = {}
gsconfig = {}

if (is_alive) win.setTimeout(
	function(){
		swarm = seiki.swarm(global.path_swarm, config.online.swarm_popen)
	}
)
 

var listener = thread.command();
listener.refresh = function() try {
	thread.delay(100)
	var ip = swarm.get_ip()
	if !swarm return ;  
	if swarm.prcs.getExitCode() == 0 return ; 
	if (mainForm.ip != ip) mainForm.ip, mainForm.edit_online_ip.text = ip, ip
} thread.invoke(function(listener) while(true) listener.refresh(), listener)

mainForm.button_game_start.oncommand = function(id,event){
	target = io.joinpath(::GamePath, "gs03.dat")
	if (mainForm.radiobutton_use_mod.checked) {
		if mainForm.listbox_mod_viewer.selIndex == 0 {
			mainForm.msgbox("您还没有选择一个MOD。",,0x30/*_MB_ICONEXCLAMATION*/)
			return ; 
		}
		var mpath = seiki.get_mods()[mainForm.listbox_mod_viewer.selIndex].fullpath
		fsys.copy(mpath, target)
	} else {
		seiki.delete_into_recycle_bin(target)
		//fsys.delete(target)
	}
	
	ini = fsys.ini(global.path_gsocini)
	
	hosted = #mainForm.edit_online_ip.text > 0
	
	if (config.online.auto_ip) {
		ini.write("Network", "IsHost", hosted ? "1" : "0")
		if (hosted) {
			ini.write("Network", "Port", 10800)	
		} else {
			var text = win.clip.read()
			var regex = preg(seiki.regex_ipv4)
			if regex.test(text) {
				var ip, port = regex.match(text)
				ini.write("Network", "Address", ip)
				ini.write("Network", "Port", port)
			}
		}
	}
	ini.write("Font", "Family", "微软雅黑")
	
	var sel = mainForm.combobox_game_size.selIndex
	var is_last = (sel == #mainForm.combobox_game_size.items)
	seiki.set_fullscreen(is_last)
	
	if (mainForm.radiobutton_offline.checked) {
		var it = seiki.game(sel)
		if (it !== null) {
			if (it.is_game()) {
				table.push(game, it)
			}
		}
		//winex.key.altClick( ,0xD/*_VK_ENTER*/)
	} else {
		table.push(gsonline, seiki.gsonline(,function(prcs) { 
			var it = seiki.game(, prcs); 
			if (it !== null) {
				table.push(game, it) 
				if (it.is_game()) {
					var sel = mainForm.combobox_game_size.selIndex
					var is_last = (sel == #mainForm.combobox_game_size.items)
					if (!is_last) it.set_size_from_list(sel)
				}
			}
		}))
	}
}

mainForm.button_game_size.oncommand = function(id,event) {
	for i, v in table.eachIndex(game) v.set_size_from_list(mainForm.combobox_game_size.selIndex)
}

mainForm.button_setting_key.oncommand = function(id,event) {
	var it = seiki.gsconfig(); if (it !== null) table.push(gsconfig, it)
}

mainForm.onClose = function(hwnd,message,wParam,lParam) try {
	swarm.prcs.process.kill()
    for(k, v in gsonline) v.prcs.kill()
	for(k, v in gsonline) v.prcs.kill()
	for(k, v in game) v.prcs.kill()
}

return win.loopMessage();